;
; u16_2bcd
; input: HL = unsigned integer to convert
; output: C:DE = 6-digit BCD number
; changes A,B,C,D,E,H,L
u16_2bcd:

	SECTION u16_2bcd
	 
	LD BC, 16*256+0 	; 10 	handle 16 bits, one bit per iteration; b=16 c=0
	LD D,C			; 4
	LD E,C			; 4
	
	ld a,h
	or l
	ret z			; return for input 0

shift:
	ADD HL,HL
	jr c,l1
	dec b
	jp shift		; shift left until carry
	
cvtLoop:
	ADD HL, HL		; 11 
l1:	LD A, E			; 4
	ADC A, A		; 4
	DAA			; 4
	LD E, A			; 4
	LD A, D			; 4
	ADC A, A		; 4
	DAA			; 4
	LD D, A			; 4
	LD A, C			; 4
	ADC A, A		; 4
	DAA			; 4
	LD C, A			; 4
	DJNZ cvtLoop		; 13/8 	72cy/iteration
	

	RET			; 10
	
	ENDSECTION u16_2bcd