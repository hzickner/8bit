; 03/22/2017	599 bytes
; 20000		11	435	455	901
; 03/22/2017	593 bytes
; 20000		11	381	455	847	partial register optimisation
; 03/22/2017	591 bytes
; 20000		11	375	454	840	jp/jr optimization
; 03/23/2017	575 bytes
; 20000		11	344	455	810	register optimization

	CPU Z80

CODEBASE	EQU 0200H

	ORG CODEBASE-80H		; 128byte file header before start of code
	
; CAOS file header
	DEFB "SIEVE",0,0,0,0,0,0	; name 11 chars!
	DEFB 5 dup (0)
	DEFB 3 				;=3 mit SS-ADR, 2 ohne
	DEFW AAMAIN
	DEFW EEMAIN
	DEFW main 			;nur bei SS
	DEFB 105 dup (0)		; fill block

; start of code
AAMAIN EQU $

; magic command header for CAOS menu
	DEFW 7F7FH
	DEFB "SIEVE"
	DEFB 01

;
; includes for EQUs and macros
	include "..\include\kc85equ.z80"
	include "..\include\crt_mac.z80"
		
NN	EQU 20000	
;
; main function	
main:
	call reset_timer
	
	ld hl,NN
	ld (u16_N),hl

	ld hl,flagbuf
	ld bc,(u16_n)
	xor a
	call memset
	
	ld hl,(timer)
	ld (u16_tm1),hl
	
	ld hl,flagbuf
	ld (hl),1		; flags[0]=1
	
	inc hl			; PTR2=flagbuf+1
	ld (ptr2),hl
	
	ld bc,02		; use bc for i
	ld e,a
	ld d,a
	ld (u16_count),de	; count=0
	
while1
	ld a,(u16_n)
	sub c
	ld a,(u16_n+1)
	sbc a,b
	jr c,while1out		; while (i <= n)
	
	ld hl,(ptr2)
	ld a,(hl)
	or a
	jp nz,else1		; if !flag[i] ; jp faster here
	
	ld hl,(u16_count)
	inc hl
	ld (u16_count),hl	; count++
	
	ld h,b
	ld l,c			; use bc for i
	add hl,hl
	ld (u16_k),hl		; k = i+i
	
	ld hl ,(ptr2)
	ld (ptr3),hl		; ptr3=ptr2
while2	
	ld a,(u16_n)
	ld de,(u16_k)
	sub e
	ld a,(u16_n+1)
	sbc a,d
	jr c,while2out		; while (k <= n) ; jr is faster here
	
	ld hl,(ptr3)
	add hl,bc
	ld (ptr3),hl		; ptr3=ptr3+i
	
	ld a,1
	ld hl,(ptr3)
	ld (hl),a
	
	ld hl,(u16_k)
	add hl,bc
	ld (u16_k),hl		; k=k+i
	jp while2
while2out
else1	
	inc bc			; i++
	ld hl,(ptr2)
	inc hl
	ld (ptr2),hl		; ptr2++
	
	jp while1
while1out	

	ld hl,(timer)
	ld (u16_tm2),hl

;
; print primes <N2
	ld hl,flagbuf
	inc hl			; PTR2=flagbuf+1
	ld (ptr2),hl
	
	ld hl,(u16_N)
	srl h
	rr l
	srl h
	rr l
	srl h
	rr l
	ld (u16_N2),hl		; N2 = N >> 3
	
	ld hl,0
	ld (u16_count2),hl
	ld l,2
	ld (u16_i),hl		; i=0; count2=2
while3
	ld a,(u16_n2)
	ld de,(u16_i)
	sub e
	ld a,(u16_n2+1)
	sbc a,d
	jr c,while3out		; while (i <= n2)
	
	ld hl,(ptr2)
	ld a,(hl)
	or a
	jp nz,else3		; jp expected faster
	
	ld hl,(u16_count2)
	inc hl
	ld (u16_count2),hl	; count2++
	ld hl,(u16_i)
	printu16
	ld a,' '
	putc			; print i

else3	
	ld hl,(u16_i)
	inc hl
	ld (u16_i),hl		; i++
	ld hl,(ptr2)
	inc hl
	ld (ptr2),hl		; ptr2++
	jp while3
while3out
	newline	

	ld hl,(timer)
	ld (u16_tm4),hl
	
	ld de,(u16_tm2)
	or a
	sbc hl,de
	ld (u16_tm3),hl		; tm3 = tm4-tm2
	ld hl,(u16_tm2)
	ld de,(u16_tm1)
	or a
	sbc hl,de
	ld (u16_tm2),hl		; tm2 = tm2-tm1
				
;
; print timings
	ld hl,(u16_N)
	printu16
	ld hl,STR3
	call putstring
	ld hl,(u16_tm1)
	printu16
	ld hl,STR2
	call putstring		; memset time
	
	ld hl,(u16_count)
	printu16
	ld hl,STR1
	call putstring
	ld hl,(u16_tm2)
	printu16
	ld hl,STR2
	call putstring		; calc time	

	ld hl,(u16_count2)
	printu16
	ld hl,STR1
	call putstring
	ld hl,(u16_tm3)
	printu16
	ld hl,STR2
	call putstring		; print time
	
	ld hl,(u16_tm4)
	printu16
	ld hl,STR4
	call putstring
		
	ret
;
; end of main

	include "..\include\timer.z80"
	include "..\include\memset.z80"
	include "..\include\putstring.z80"
	include "..\include\u16_2bcd.z80"
	include "..\include\printbcd20.z80"
	
;
; constants
STR1	DB " PRIMES IN",CR,LF,0
STR2	DB " TICKS",CR,LF,0
STR3	DB " BYTES CLEARED IN",CR,LF,0
STR4	DB " TICKS TOTAL",CR,LF,0
	
; end of code	
EEMAIN EQU $

;
; Variables not initialized
flagbuf		DS NN
ptr2		DS 2
ptr3		DS 2	
timer		DS 2
u16_N		DS 2
u16_N2		DS 2
u16_i		DS 2
u16_k		DS 2
u16_count	DS 2
u16_count2	DS 2
u16_tm1 	DS 2
u16_tm2 	DS 2
u16_tm3 	DS 2
u16_tm4 	DS 2

;
; fill last 128byte block with 0
	ORG EEMAIN			; reset ORG to end of code 
EELAST EQU ($+127)&0FF80H
FILL	EQU EELAST-EEMAIN
	REPT FILL
	DEFB 0
	ENDM
	
	outradix 10
	message "Codesize: \{EEMAIN-AAMAIN}"
			